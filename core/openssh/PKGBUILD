# $Id: PKGBUILD 89278 2010-08-30 21:38:00Z thomas $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=openssh
pkgver=5.6p1
pkgrel=3
pkgdesc='A Secure SHell server/client'
arch=('i686' 'x86_64' arm)
license=('custom')
url="http://www.openssh.org/portable.html"
backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd')
depends=('openssl' 'zlib')
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${pkgver}.tar.gz"
        'openssh' 'sshd.pam')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  #NOTE we disable-strip so that makepkg can decide whether to strip or not
  ./configure --prefix=/usr --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh --with-privsep-user=nobody \
    --with-md5-passwords --with-mantype=man --mandir=/usr/share/man \
    --with-xauth=/usr/bin/xauth --with-ssl-engine \
    --disable-strip
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/openssh ${pkgdir}/etc/monit.d/openssh

  install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE
  install -Dm644 ${srcdir}/sshd.pam ${pkgdir}/etc/pam.d/sshd

  rm ${pkgdir}/usr/share/man/man1/slogin.1
  ln -sf ssh.1.gz ${pkgdir}/usr/share/man/man1/slogin.1.gz

  #additional contrib scripts that we like
  install -Dm755 contrib/findssl.sh ${pkgdir}/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id ${pkgdir}/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1  ${pkgdir}/usr/share/man/man1/ssh-copy-id.1

  # sshd_config
  sed -i \
    -e 's|^#ListenAddress 0.0.0.0|ListenAddress 0.0.0.0|g' \
    -e 's|^#UsePAM no|UsePAM yes|g' \
    -e 's|^#ChallengeResponseAuthentication yes|ChallengeResponseAuthentication no|g' \
    ${pkgdir}/etc/ssh/sshd_config
  echo "HashKnownHosts yes" >>  ${pkgdir}/etc/ssh/ssh_config
  echo "StrictHostKeyChecking ask" >>  ${pkgdir}/etc/ssh/ssh_config

  #ssh_config
  sed -i \
    -e 's|^# Host \*|Host *|g' \
    ${pkgdir}/etc/ssh/ssh_config
}

sha1sums=('347dd39c91c3529f41dae63714d452fb95efea1e'
          'b5b9615db97d1b171735028d555f46191508fb76'
          '0d242e32e13a9cda4004c07e6930d2e1c5131cfe')
sha256sums=('538af53b2b8162c21a293bb004ae2bdb141abd250f61b4cea55244749f3c6c2b'
            '5133c484318ddae04b3483ec8f4ced1fa1a19629522f0a77a55639272d79ea52'
            'fa82d17743a712623d3f2aa1e0a6aeb3dc6d3bbcac75ba19560b4c1405a7a709')
