# $Id: PKGBUILD 35422 2010-12-20 11:42:45Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.17.4
pkgrel=3
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64" "arm")
provides=('sh' 'sed' 'tar' 'awk' 'vi' 'less' 'grep' 'diffutils' 'coreutils')
url="http://busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
options=()
source=($url/downloads/$pkgname-$pkgver.tar.bz2 \
	config symlinks.sh)
md5sums=('b3254232e9919007ca803d3a4fe81f3c'
         '8d4c8f627993d2f06b6c1ca19810a4c9'
         'df387b71d660834fc0d0f744a17075bb')


build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/config .config
  # if you want to run menuconfig uncomment the following line:
  # make menuconfig ; return 1
  make
  mkdir $pkgdir/bin
  install -m755 busybox $pkgdir/bin/
  
  mkdir $pkgdir/sbin
  install -m755 -T busybox $pkgdir/sbin/login
  install -m755 -T busybox $pkgdir/sbin/init

  cd $pkgdir/bin
  source $srcdir/symlinks.sh
  for x in $LINKS; do
    ln -s busybox $x
  done
}
